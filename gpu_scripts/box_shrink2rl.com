#!/bin/tcsh -f
#####**************************************************************************#####
#Despcription: This program is used to shrink box files in EMAN1 format
#Copyright@MRC-LMB
#Author: Kai Zhang
#Last Edit: 2014-3-29

#####**************************************************************************#####

#global setup for output format
set KBold="\x1b\x5b1m"
set KDefault="\x1b\x5b0m"
set KUnderline="\x1b\x5b4m"
set KFlash="\x1b\x5b5m"

#end of global setup

set args = `printf "$argv" | wc | awk '{print $2}'`
set Proc_name=`echo $0 | awk '{n=split($1,scr,"/");print scr[n];}'`

if ( $args <= 1 || $1 == '--help' || $1 == '-h' ) then


        printf "${KBold}Despcription: ${KDefault}This program is used to shrink box files in EMAN1 format  (with tow additional column, class number and cross-correlation generated by AutoMatch) to Relion star file .\n"
        printf "              It's quite useful when you have box files in the original micrographs and want to use them in the shrinked micrographs.\n"
        printf "              In this case, the shrink factor is always larger than 1, for example 2, 3 or 4.\n"
        printf "              Make sure the box file contains 6 columns, X, Y, Xsize, Ysize, Cross-correlation, ClassNumber.If the box files contain only X, Y, Xsize, Ysize, please use box_shrink2rl_XYonly.com instead.\n"

        printf "${KBold}Usage:${KDefault}   $Proc_name <shrink factor> <box file1> [box file2] [...] \n"
        printf "${KBold}example:${KDefault} $Proc_name 2 data_1001.box \n"
        printf "${KBold}example:${KDefault} $Proc_name 4 data_1001.box data_1002.box\n"

        exit(1)
endif

set shrink=$argv[1]

set i=2

while ($i <= $args )

set box=$argv[$i]
set root=`basename $box .box`
set starf=${root}_shrink${shrink}.star

if ( -f ${root}.box) then
echo "" > $starf
echo "data_" >> $starf
echo "" >> $starf
echo "loop_" >> $starf
echo "_rlnCoordinateX #1" >> $starf
echo "_rlnCoordinateY #2" >> $starf
echo "_rlnClassNumber #3" >> $starf
echo "_rlnAutopickFigureOfMerit #4" >> $starf

gawk '//{printf("%d\t%d\t%d\t%.3f\n", ($1+ $3/2)/'$shrink'  , ($2 + $4/2) / '$shrink', $6 , $5)}' $box >>  $starf

echo "shrinking $argv[$i] to $starf "
else
echo "$starf does not exists, or wrong format or suffix not .box"
endif

@ i++

end

exit
