#!/bin/csh -f

#####**************************************************************************#####
#Despcription: This program is used to convert the box files in EMAN1 format to a relion star file
#Copyright@MRC-LMB
#Author: Kai Zhang
#Last Edit: 2014-4-11

#####**************************************************************************#####

#global setup for output format
set KBold="\x1b\x5b1m"
set KDefault="\x1b\x5b0m"
set KUnderline="\x1b\x5b4m"
set KFlash="\x1b\x5b5m"

#end of global setup

set args = `printf "$argv" | wc | awk '{print $2}'`
set Proc_name=`echo $0 | awk '{n=split($1,scr,"/");print scr[n];}'`

if ( $args < 1 || $1 == '--help' || $1 == '-h' ) then


        printf "${KBold}Despcription: ${KDefault}This program is used to convert the box files in EMAN1 format (with tow additional column, class number and cross-correlation generated by AutoMatch) to a relion star file.\n"
        printf "              Make sure the box file contains 6 columns, X, Y, Xsize, Ysize, Cross-correlation, ClassNumber.If the box files contain only X, Y, Xsize, Ysize, please use box2rl_XYonly.com instead.\n"
        printf "${KBold}example:${KDefault} $Proc_name  data_1???.box\n"

        exit(1)
endif




set i=1

while ($i <= $args )

set box=$argv[$i]
set root=`basename $box .box`

if ( -f ${root}.box  && -Z ${root}.box ) then
#<<< if 1001

set starf=${root}_box2rl.star



echo "convert $box to $starf "

echo "" > $starf
echo "data_" >> $starf
echo "" >> $starf
echo "loop_" >> $starf
echo "_rlnCoordinateX #1" >> $starf
echo "_rlnCoordinateY #2" >> $starf
echo "_rlnClassNumber #3" >> $starf
echo "_rlnAutopickFigureOfMerit #4" >> $starf

gawk '//{printf("%d\t%d\t%d\t%.3f\n", ($1+ $3/2)  , ($2 + $4/2), $6 , $5)}' $box >>  $starf



else
#<<< if 1001

echo "$box does not exists, or zero size or wrong format or suffix not .box"

endif
#>>> if 1001

@ i++

end


exit

